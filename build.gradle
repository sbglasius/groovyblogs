buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        mavenCentral()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:hibernate5:7.3.0"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
        classpath 'org.grails.plugins:database-migration:4.1.0'
    }
}

plugins {
    id 'com.palantir.docker' version '0.34.0'
    id "com.magnetichq.client-dependencies" version "2.0.0"
}

version "5.0.0"
group "org.groovyblogs"

apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.bertramlabs.asset-pipeline"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "io.micronaut:micronaut-inject-groovy"
    console "org.grails:grails-console"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-plugin-i18n"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:hibernate5"
    implementation "org.hibernate:hibernate-core:5.6.9.Final"
    implementation "org.grails.plugins:events"
    implementation "org.grails.plugins:gsp"
    profile "org.grails.profiles:web"
    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-remote-driver:3.141.59"
    testImplementation "org.seleniumhq.selenium:selenium-api:3.141.59"
    testImplementation "org.seleniumhq.selenium:selenium-support:3.141.59"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:3.141.59"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:3.141.59"

    implementation 'com.rometools:rome:1.16.0'
    implementation 'com.commsen:jwebthumb:0.3'
    implementation 'org.jfree:jfreechart:1.0.19'
    implementation 'net.homeip.yusuke:twitter4j-core:2.1.0'
    implementation 'org.grails.plugins:i18n-enums:2.0.2'
    implementation 'org.grails.plugins:spring-security-core:4.0.3'
    implementation 'com.google.code.maven-play-plugin.spy:memcached:2.4.2'
    implementation 'org.grails:grails-datastore-rest-client:6.1.12.RELEASE'
    implementation 'org.grails.plugins:recaptcha:3.2.0'
    implementation 'dk.glasius:external-config:3.0.0'
    implementation "org.grails:grails-plugin-codecs"
    implementation 'org.codehaus.groovy:groovy-dateutil'
    implementation 'org.grails.plugins:quartz:2.0.13'
    implementation("org.quartz-scheduler:quartz:2.2.3") {
        exclude module: 'slf4j-api'
        exclude module: 'c3p0'
    }
    implementation 'org.grails.plugins:database-migration:4.1.0'
    implementation 'org.liquibase:liquibase-core:4.8.0'
    
    runtimeOnly "org.grails.plugins:async:4.0.0"
    runtimeOnly "org.grails:grails-events-rxjava2:4.0.0"
    runtimeOnly 'mysql:mysql-connector-java:8.0.25'

    assets 'com.bertramlabs.plugins:less-asset-pipeline:3.3.4'

    testImplementation "org.grails:grails-plugin-codecs"
}

sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}

clientDependencies {
    fileExtensions 'less'
    npm {
        'jquery'('3.6.0') 
        'bootstrap-less'('3.3.8') 
        'font-awesome'('^4.7')
    }
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}


assets {
    minifyJs = true
    minifyCss = true

    includes = ['**/custom-bootstrap.less']
    excludes = ['**/*.less']
}

docker {
    name "docker.gr8conf.org/groovyblogs:${project.version}"
    tag "groovyblogs", "docker.gr8conf.org/groovyblogs"
    copySpec.into('app').from(tasks.bootJar.outputs) {
            rename { fileName -> fileName - "-${project.version}" }
        }
    copySpec.into('app').from('src/main/docker/app')
    dockerfile file('src/main/docker/Dockerfile')
    pull true
    noCache true
}